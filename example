#!/usr/bin/env perl
use 5.036;
use utf8;
use open ':std', ':encoding(UTF-8)';
use Sq -sig => 1;

# read info about examples - when file doesn't exists $info will be empty hash
my $info =
    Sq->fs
    ->read_text('examples', 'info.txt')
    ->trim
    ->rxm(qr/\A \s*+ (?= [^#] ) (\S++) \s++ (.++)\z/x)
    ->merge->as_hash;

# built array of array with info
my $examples =
    Sq->fs
    ->children('examples')
    ->keep    (call 'is_file')
    ->keep    (sub($str) { Str->ends_with($str->stringify, '.pl') })
    ->sort    (by_str)
    ->choose  (sub($file) {
        my $name = $file->basename;
        return None if $name eq 'info.txt';
        return Some(sq [$name, $info->{$name} // ""]);
    });

# $Sq::Dump::Inline = 120;
# dump($examples);

# print Array of Array as Table
Sq->fmt->table({
    header => [qw/Script Description/],
    data   => $examples,
    border => 0,
});
